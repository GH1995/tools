
自动加载
===========

如何自动加载
------------------

Vim使用称为"自动加载(autoload)"来解决这个问题。自动加载让你直到需要时才加载某一部分代码。

    :call somefile#Hello()

当你执行这个命令，Vim的行为与平常的函数调用有些许不同。

如果这个函数已经加载了，Vim简单地像平常一样调用它。

否则Vim将在你的`~/.vim`(或`～/.vim/bundles/对应的插件/autoload`)下查找一个叫做`autoload/somefile.vim`的文件。

在这个文件内，函数应该这样定义：

    function somefile#Hello()
        " ...
    endfunction

你可以在函数名中使用多个`#`来表示子目录。举个例子：

    :call myplugin#somefile#Hello()

这将在`autoload/myplugin/somefile.vim`查找自动加载文件。
里面的函数需要使用自动加载的绝对路径进行定义：

    function myplugin#somefile#Hello()
        " ...
    endfunction

实验一下
-------------

创建一个`~/.vim/autoload/example.vim`文件并加入下面的内容：

保存文件并执行`:call example#Hello()`。Vim将输出下面内容：

1. Vim的确是在半途加载了`example.vim`文件。当我们打开Vim的时候它并不存在，所以不可能是在启动时加载的。
2. 当Vim找到它需要自动加载的文件后，它在调用对应函数之前就加载了整个文件。

**先不要关闭Vim**，修改函数的定义成这样：

    function! example#Hello()
        echom "Hello AGAIN, world!"
    endfunction

    echom "Done loading."

Vim已经有了`example#Hello`的一个定义，所以它不再需要重新加载文件，这意味着：

1. 函数以外的代码将不再执行。
2. 它不会反映函数本身的变化。

现在执行`:call example#BadFunction()`。你将再一次看到加载信息，伴随着一个函数不存在的错误。
但现在尝试再次执行`:call example#Hello()`。这次你将看到更新后的信息！

目前为止你应该清晰地了解到Vim会怎么处理一个自动加载类型的函数调用吧：

1. 它首先是否已经存在同名的函数了。如果是，就调用它。
2. 否则，查找名字对应的文件，并source它。
3. 然后试图调用那个函数。如果成功，太棒了。如果失败，就输出一个错误。

自动加载什么
----------------

正因为如此，如果你不是写一个用户会在*每次*打开Vim对话时都用到的插件，最好尽量把功能代码都挪到autoload文件中去。

在Potion插件里添加自动加载
---------------------------------------

在前一章的最后，我们的`ftplugin/potion/running.vim`文件大概是这样：

    if !exists("g:potion_command")
        let g:potion_command = "/Users/sjl/src/potion/potion"
    endif

    function! PotionCompileAndRunFile()
        silent !clear
        execute "!" . g:potion_command . " " . bufname("%")
    endfunction

    function! PotionShowBytecode()
        " Get the bytecode.
        let bytecode = system(g:potion_command . " -c -V " . bufname("%"))

        " Open a new split and set it up.
        vsplit __Potion_Bytecode__
        normal! ggdG
        setlocal filetype=potionbytecode
        setlocal buftype=nofile

        " Insert the bytecode.
        call append(0, split(bytecode, '\v\n'))
    endfunction

    nnoremap <buffer> <localleader>r :call PotionCompileAndRunFile()<cr>
    nnoremap <buffer> <localleader>b :call PotionShowBytecode()<cr>

但你可以想象到可能有那么一个插件包括了数千行可以通过自动加载来减少每次的加载时间的代码。

让我们开始吧。在你的插件repo中创建一个`autoload/potion/running.vim`文件。

    echom "Autoloading..."

    function! potion#running#PotionCompileAndRunFile()
        silent !clear
        execute "!" . g:potion_command . " " . bufname("%")
    endfunction

    function! potion#running#PotionShowBytecode()
        " Get the bytecode.
        let bytecode = system(g:potion_command . " -c -V " . bufname("%"))

        " Open a new split and set it up.
        vsplit __Potion_Bytecode__
        normal! ggdG
        setlocal filetype=potionbytecode
        setlocal buftype=nofile

        " Insert the bytecode.
        call append(0, split(bytecode, '\v\n'))
    endfunction

注意`potion#running`部分的函数名怎么匹配它们所在的路径。

现在修改`ftplugin/potion/running.vim`文件成这样：

    if !exists("g:potion_command")
        let g:potion_command = "/Users/sjl/src/potion/potion"
    endif

    nnoremap <buffer> <localleader>r
                \ :call potion#running#PotionCompileAndRunFile()<cr>

    nnoremap <buffer> <localleader>b
                \ :call potion#running#PotionShowBytecode()<cr>

正如你看到的，我们保留`nnoremap`映射部分不变。
我们不能自动加载它们，不然用户就没办法引发自动加载了！

你将在Vim插件中普遍看到：大多数的功能将位于自动加载函数中，仅有`nnoremap`和`command`命令每次都被Vim加载。

